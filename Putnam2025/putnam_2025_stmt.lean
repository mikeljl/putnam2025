import Mathlib
open Classical

/--
Let $m_0$ and $n_0$ be distinct positive integers. For every positive integer $k$,
define $m_k$ and $n_k$ to be the relatively prime positive integers such that
$$\frac{m_k}{n_k} = \frac{2m_{k-1} + 1}{2n_{k-1} + 1}.$$
Prove that $2m_k + 1$ and $2n_k + 1$ are relatively prime for all but finitely many
positive integers $k$.
-/
theorem putnam_2025_a1 (m n : ℕ → ℕ)
  (h0 : m 0 > 0 ∧ n 0 > 0 ∧ m 0 ≠ n 0)
  (hm : ∀ k : ℕ, m (k + 1) = ((2 * m k + 1) / (2 * n k + 1) : ℚ).num)
  (hn : ∀ k : ℕ, n (k + 1) = ((2 * m k + 1) / (2 * n k + 1) : ℚ).den):
  {k | ¬ (2 * m k + 1).Coprime (2 * n k + 1)}.Finite := by sorry

open Set Real in
/--
Find the largest real number $a$ and the smallest real number $b$ such that
$$ax(\pi - x) \le \sin x \le bx(\pi - x)$$
for all $x$ in the interval $[0, \pi]$.
-/
theorem putnam_2025_a2 :
  IsGreatest {a : ℝ | ∀ x ∈ Icc 0 π, a * x * (π - x) ≤ sin x} (1 / π) ∧
  IsLeast {b : ℝ | ∀ x ∈ Icc 0 π, b * x * (π - x) ≥ sin x} (4 / π ^ 2) := by sorry


abbrev State (n : ℕ) : Type := (Fin n → Fin 3) × Finset (Fin n → Fin 3)
abbrev State.validNext {n : ℕ} (s : State n) : Finset (State n) :=
  {s' |
    s'.1 ∈ s.2 ∧
    (∃ (i : Fin n) (δ : ({1, -1} : Finset ℤ)), s'.1 i = s.1 i + δ.val ∧ ∀ j ≠ i, s'.1 j = s.1 j) ∧
    s'.2 = s.2.erase s'.1}
abbrev State.init (n : ℕ) : State n := ⟨fun _ ↦ 0, Finset.univ.erase (fun _ ↦ 0)⟩
abbrev Strategy (n : ℕ) : Type := State n → State n
abbrev IsRollout {n : ℕ} (a b : Strategy n) (s : ℕ → State n) : Prop :=
  s 0 = State.init n ∧
  (∀ i, Even i → s (i + 1) = a (s i)) ∧
  (∀ i, Odd i → s (i + 1) = b (s i))


open Finset in
/--
Alice and Bob play a game with a string of $n$ digits, each of which is restricted
to be 0, 1, or 2. Initially all the digits are 0. A legal move is to add or subtract 1
from one digit to create a new string that has not appeared before. A player with no
legal move loses, and the other player wins. Alice goes first, and the players alternate
moves. For each $n \ge 1$, determine which player has a strategy that guarantees winning.
-/
theorem putnam_2025_a3 (n : ℕ)
  (hn : 1 ≤ n):
  ∃ b : Strategy n, ∀ a : Strategy n,
    ∀ s : ℕ → State n, IsRollout a b s →
      ∃ k : ℕ, Even k ∧ (∀ i < k, s (i + 1) ∈ (s i).validNext) ∧ s (k + 1) ∉ (s k).validNext := by sorry

/--
Find the minimal value of $k$ such that there exist $k$-by-$k$ real matrices
$A_1, \ldots, A_{2025}$ with the property that $A_i A_j = A_j A_i$ if and only if
$|i - j| \in \{0, 1, 2024\}$.
-/
theorem putnam_2025_a4 :
  IsLeast {k : ℕ | k > 0 ∧ ∃ A : Fin 2025 →
    Matrix (Fin k) (Fin k) ℝ, ∀ i j, A i * A j = A j * A i ↔ |(i : ℤ) - (j : ℤ)| ∈ ({0, 1, 2024} : Set ℤ)} 3 := by sorry


/--
Let $b_0 = 0$ and, for $n \ge 0$, define $b_{n+1} = 2b_n^2 + b_n + 1$.
For each $k \ge 1$, show that $b_{2^{k+1}} - 2b_{2^k}$ is divisible by $2^{2k+2}$
but not by $2^{2k+3}$.
-/
theorem putnam_2025_a6 (b : ℕ → ℤ)
  (hb0 : b 0 = 0)
  (hb : ∀ n, b (n + 1) = 2 * b n ^ 2 + b n + 1)
  (k : ℕ)
  (hk : k ≥ 1):
  padicValInt 2 (b (2 ^ (k + 1)) - 2 * b (2 ^ k)) = 2 * k + 2 := by sorry


open EuclideanGeometry Affine Simplex in
/--
Suppose that each point in the plane is colored either red or green, subject to the following
condition: For every three noncollinear points $A$, $B$, $C$ of the same color, the center of the
circle passing through $A$, $B$, $C$ is also this color. Prove that all points of the plane are the
same color.
-/
theorem putnam_2025_b1 (c : EuclideanSpace ℝ (Fin 2) → Bool)
  (hc : ∀ t : Triangle ℝ (EuclideanSpace ℝ (Fin 2)), c (t.points 0) = c (t.points 1) → c (t.points 0) = c (t.points 2) →
    c (t.points 0) = c (circumcenter t)):
  ∃ c0 : Bool, ∀ A, c A = c0 := by sorry


open BigOperators Real Nat Topology Rat MeasureTheory Set Real in
/--
Let $f: [0,1] \to [0, \infty)$ be strictly increasing and continuous.
Let $R$ be the region bounded by $x = 0$, $x = 1$, $y = 0$, and $y = f(x)$.
Let $x_1$ be the $x$-coordinate of the centroid of $R$.
Let $x_2$ be the $x$-coordinate of the centroid of the solid generated by rotating $R$ about the $x$-axis.
Prove that $x_1 < x_2$.
-/
theorem putnam_2025_b2 (f : ℝ → ℝ)
  (hf : ∀ x ∈ Icc 0 1, 0 ≤ f x)
  (hfm : StrictMono f)
  (hfc : Continuous f):
  let R : Set (ℝ × ℝ) := { x | 0 ≤ x.1 ∧ x.1 ≤ 1 ∧ 0 ≤ x.2 ∧ x.2 ≤ f x.1 }
  let x₁ : ℝ := ⨍ x in R, x.1
  let R' : Set (ℝ × ℝ × ℝ) := { x' | ∃ x ∈ R, ∃ θ : ℝ, x'.1 = x.1 ∧ x'.2.1 = x.2 * cos θ ∧ x'.2.2 = x.2 * sin θ }
  let x₂ : ℝ := ⨍ x' in R', x'.1
  x₁ < x₂ := by sorry


/--
Suppose $S$ is a nonempty set of positive integers with the property that if $n$ is in $S$,
then every positive divisor of $2025^n - 15^n$ is in $S$. Must $S$ contain all positive integers?
-/
theorem putnam_2025_b3 (S : Set ℕ)
  (hS : S.Nonempty)
  (h0 : ∀ n ∈ S, 0 < n)
  (h : ∀ n ∈ S, ∀ d : ℕ, 0 < d → d ∣ 2025 ^ n - 15 ^ n → d ∈ S)
  (n : ℕ)
  (hn : 0 < n):
  n ∈ S := by sorry


def MatrixProp (n : ℕ) (A : ℕ → ℕ → ℕ) : Prop :=
  (∀ i j : ℕ, 1 ≤ i → i ≤ n → 1 ≤ j → j ≤ n → i + j ≤ n → A i j = 0) ∧
  (∀ i j : ℕ, 1 ≤ i → i ≤ n - 1 → 1 ≤ j → j ≤ n →
    A (i + 1) j = A i j ∨ A (i + 1) j = A i j + 1) ∧
  (∀ i j : ℕ, 1 ≤ i → i ≤ n → 1 ≤ j → j ≤ n - 1 →
    A i (j + 1) = A i j ∨ A i (j + 1) = A i j + 1)

def colSum (n : ℕ) (A : ℕ → ℕ → ℕ) (j : ℕ) : ℕ :=
  Finset.sum (Finset.range n) (fun i => A (i + 1) j)

def colNonzeros (n : ℕ) (A : ℕ → ℕ → ℕ) (j : ℕ) : ℕ :=
  ((Finset.range n).filter (fun i => A (i + 1) j ≠ 0)).card

def totalSum (n : ℕ) (A : ℕ → ℕ → ℕ) : ℕ :=
  Finset.sum (Finset.range n) (fun j => colSum n A (j + 1))

def nonzeroCount (n : ℕ) (A : ℕ → ℕ → ℕ) : ℕ :=
  Finset.sum (Finset.range n) (fun j => colNonzeros n A (j + 1))

/--
For $n \geq 2$, let $A = [a_{i,j}]_{i,j=1}^n$ be an $n$-by-$n$ matrix of nonnegative integers such that:
(a) $a_{i,j} = 0$ when $i + j \leq n$;
(b) $a_{i+1,j} \in \{a_{i,j}, a_{i,j} + 1\}$ when $1 \leq i \leq n-1$ and $1 \leq j \leq n$; and
(c) $a_{i,j+1} \in \{a_{i,j}, a_{i,j} + 1\}$ when $1 \leq i \leq n$ and $1 \leq j \leq n-1$.

Let $S$ be the sum of the entries of $A$, and let $N$ be the number of nonzero entries of $A$.
Prove that $S \leq \frac{(n+2)N}{3}$.
-/
theorem putnam_2025_b4 (n : ℕ)
  (hn : 2 ≤ n)
  (A : ℕ → ℕ → ℕ)
  (h : MatrixProp n A):
  3 * totalSum n A ≤ (n + 2) * nonzeroCount n A := by sorry


open Finset ZMod Classical in
/--
Let $p$ be a prime number greater than 3. For each $k \in \{1, \ldots, p-1\}$,
let $I(k) \in \{1, 2, \ldots, p-1\}$ be such that $k \cdot I(k) \equiv 1 \pmod{p}$.
Prove that the number of integers $k \in \{1, \ldots, p-2\}$ such that
$I(k+1) < I(k)$ is greater than $p/4 - 1$.
-/
theorem putnam_2025_b5 (p : ℕ)
  [hp : Fact p.Prime]
  (hp3 : 3 < p):
  (#{k : ZMod p | k ≠ 0 ∧ k ≠ -1 ∧ (k + 1)⁻¹.val < k⁻¹.val} : ℝ) > p / 4 - 1 := by sorry

/--
Let $\mathbb{N} = \{1, 2, 3, \ldots\}$. Find the largest real constant $r$ such that
there exists a function $g: \mathbb{N} \to \mathbb{N}$ such that
$$g(n+1) - g(n) \geq (g(g(n)))^r$$
for all $n \in \mathbb{N}$.
-/
theorem putnam_2025_b6 :
  IsGreatest {r : ℝ | ∃ g : ℕ+ → ℕ+, ∀ n : ℕ+, (g (n + 1) : ℝ) - (g n : ℝ) ≥ (g (g n) : ℝ) ^ r} (1/4 : ℝ) := by sorry
